/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_ardun.skrit

	Author(s)	: 	Eric Tams, Chad Queen, Bartosz Kijanka

	Purpose		: 	Custom talk job for a talking guard part of the Apprentice Ardun quest.

	(C)opyright 2001 Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go		m_Go$;
GoMind	m_Mind$;
GoBody  m_Body$;
Job		m_Job$;
Goid	m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init
event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
}

event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Talk$;
}

////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Lee on coffee
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END );
	}

	event OnEnterState$
	{
		#only( game )
		[[
			if( (Goid.InValidGoid != UIPartyManager.DoesActivePartyHaveTemplate( "book_glb_lore_altair_3", m_Target$ )) && !GameAuditor.GetDb.GetBool( "guard_gave_book" ) )
			{
				if( m_Target$.IsValid() ) 
				{	
					GameAuditor.GetDb.SetBool( "guard_gave_book", true );
					
					SiegePos SpawnPos$;
					
					// make an amulet
					GoCloneReq cloneReq$ = MakeGoCloneReq( "book_glb_lore_altair_1" );
					
					SpawnPos$ = m_Target$.go.placement.position;
					cloneReq$.StartingPos	= SpawnPos$;
				    
					If( m_Target$.Go.Hasmind )
					{
						if( AIQuery.FindSpotRelativeToSource( m_Target$.Go, 1, 1.5, 2.0, m_Target$.Go.Mind.TempPos1 ) )
				   		{
				   			cloneReq$.StartingPos = m_Target$.Go.Mind.TempPos1;
				   		}
				   	}	
					cloneReq$.SnapToTerrain = true;
			        GoDb.SCloneGo( cloneReq$ );
				}
				
				m_Go$.GetConversation.RSSelectConversation( "conversation_book1" );
				TryAnim$('talk');
			} else {
				m_Go$.GetConversation.RSSelectConversation( "conversation_archer_guard2" );
				TryAnim$('talk');
			}
		]]		

		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		m_Job$.MarkForDeletion();
	}
}
