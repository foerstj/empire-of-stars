Go			m_Go$;
GoMind		m_Mind$;
GoBody  	m_Body$;
Job			m_Job$;
Goid		m_Target$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"


property    string  namespace$      = "book0"               doc = "Namespace for conversations and flags";
property    string  object_template$ = "book_glb_lore_altair_0" doc = "Object to give";
property    bool    face_player$    = true                  doc = "Face player when starting to talk";
property    bool    face_back$      = false                 doc = "Face back when talk is finished";


float actor_X$;
float actor_Y$;
float actor_Z$;
float actor_W$;


bool give_object$( goid target$, string templateName$ )
{
    if( !target$.IsValid() )
    {
        return false;
    }

    SiegePos SpawnPos$;
    GoCloneReq cloneReq$ = MakeGoCloneReq( templateName$ );
    
    SpawnPos$ = target$.go.placement.position;
    cloneReq$.StartingPos	= SpawnPos$;
    
    if( target$.Go.Hasmind )
    {
        if( AIQuery.FindSpotRelativeToSource( target$.Go, .25, .5, 2.0, target$.Go.Mind.TempPos1 ) )
        {
            cloneReq$.StartingPos = target$.Go.Mind.TempPos1;
        }
    }	
    cloneReq$.SnapToTerrain = true;
    GoDb.SCloneGo( cloneReq$ );
    
    return true;
}

startup state STARTUP$
{
}

event OnJobInit$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
	m_Target$	= job$.GoalObject;

    if ( face_player$ )
    {
        actor_X$ = m_go$.Placement.Orientation.x;
		actor_Y$ = m_go$.Placement.Orientation.y;
		actor_Z$ = m_go$.Placement.Orientation.z;
		actor_W$ = m_go$.Placement.Orientation.w;

		SetState Face$;
    }
    else
    {
        SetState Talk$;
    }
}

state Face$
{
	transition
	{
		-> Talk$:	OnWorldMessage( WE_MCP_FACING_LOCKEDON );
	}

	event OnEnterState$
	{
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Target$ );
	}
}

state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			if( m_Go$.Mind.ActorAutoFidgets )
			{
				StartFidgetIfRequired$();
			}
		}
	}

	trigger OnWorldMessage$( WE_ANIM_DONE )
	{
		TryAnim$('talk');
	}

	event OnEnterState$
	{
		#only( game )
		[[
			if( ( Goid.InValidGoid != UIPartyManager.DoesActivePartyHaveTemplate( "book_glb_lore_altair_3", m_Target$ ) ) && !GameAuditor.GetDb.GetBool( MakeStringF( "gave_%s", namespace$ ) ) )
			{
                GameAuditor.GetDb.SetBool( MakeStringF( "gave_%s", namespace$ ), true );
                give_object$( m_Target$, object_template$ );
				m_Go$.GetConversation.RSSelectConversation( MakeStringF( "conversation_%s_give", namespace$ ) );
			}
            else
            {
				m_Go$.GetConversation.RSSelectConversation( MakeStringF( "conversation_%s_no_quest", namespace$ ) );
			}
		]]

		m_Go$.GetConversation.RSActivateDialogue();

        TryAnim$('talk');
	}
}

state Exiting$
{
	event OnEnterState$
	{
        if ( face_player$ )
        {
            MCPManager.MakeRequest( m_Go$.Goid, PL_FACE );

            if ( face_back$ )
            {
                m_go$.Placement.SSetOrientation( MakeQuat( actor_X$, actor_Y$, actor_Z$, actor_W$));
            }
        }

		m_Job$.MarkForDeletion();
	}
}
