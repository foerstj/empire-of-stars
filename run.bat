:: This script is supposed to be executed from your DS installation folder.
:: TankCreator is expected to be in a sibling dir.

:: map name, case sensitive
set map_cs=EoS
:: map name, lowercase
set map=eos
:: path of vanilla DS1 documents dir (where map gets saved into)
set doc_ds=%USERPROFILE%\Documents\Dungeon Siege
:: path of DSLOA documents dir (where Bits are)
set doc_dsloa=%USERPROFILE%\Documents\Dungeon Siege LoA
:: path of DS installation
set ds=.
:: path of TankCreator
set tc=..\TankCreator

:: Copy map generated by Siege Editor
copy "%doc_ds%\Maps\%map%.dsmap" "%ds%\DSLOA\%map_cs%.dsmap"
if %errorlevel% neq 0 exit /b

:: Compile main resource file
rmdir /S /Q "%tc%\Bits"
:: exclude .psd that might be present locally; exclude nn files
robocopy "%doc_dsloa%\Bits\art" "%tc%\Bits\art" /E /xf *.psd /xf *-nn.raw
robocopy "%doc_dsloa%\Bits\sound" "%tc%\Bits\sound" /E
:: in case where I have other resources in Bits as well, I made a subfolder
robocopy "%doc_dsloa%\Bits\world\ai\jobs\%map%" "%tc%\Bits\world\ai\jobs\%map%" /E
robocopy "%doc_dsloa%\Bits\world\contentdb\components" "%tc%\Bits\world\contentdb\components" /E
robocopy "%doc_dsloa%\Bits\world\contentdb\templates\%map%" "%tc%\Bits\world\contentdb\templates\%map%" /E
robocopy "%doc_dsloa%\Bits\world\global\moods\%map%" "%tc%\Bits\world\global\moods\%map%" /E
robocopy "%doc_dsloa%\Bits\world\global\effects" "%tc%\Bits\world\global\effects" /E
%tc%\RTC.exe -source "%tc%\Bits" -out "%ds%\DSLOA\%map_cs%.dsres"
if %errorlevel% neq 0 pause
rmdir /S /Q "%tc%\Bits"

:: Compile German language resource file
robocopy "%doc_dsloa%\Bits\language" "%tc%\Bits\language" /E
%tc%\RTC.exe -source "%tc%\Bits" -out "%ds%\DSLOA\%map_cs%-de.dsres"
if %errorlevel% neq 0 pause
rmdir /S /Q "%tc%\Bits"

:: Compile non-nude overlay resource file
:: (uncomment these if those polygon boobs are too much for you)
::robocopy "%doc_dsloa%\Bits\art" "%tc%\Bits\art" *-nn.raw /S
::pushd "%tc%\Bits\art\bitmaps\characters\body_armor\type1"
::setlocal enableDelayedExpansion
::for %%F in (*-nn.raw) do (
::  set "name=%%F"
::  ren "!name!" "!name:-nn=!"
::)
::popd
::%tc%\RTC.exe -source "%tc%\Bits" -out "%ds%\DSLOA\%map_cs%-nn.dsres"
::if %errorlevel% neq 0 pause
::rmdir /S /Q "%tc%\Bits"

::pause

:: Run it!
"%ds%\DSLOA.exe" nointro=true map=%map%

:: Cleanup resources so as not to confuse Siege Editor
del "%ds%\DSLOA\%map_cs%.dsres"
del "%ds%\DSLOA\%map_cs%-de.dsres"
del "%ds%\DSLOA\%map_cs%-nn.dsres"
